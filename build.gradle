plugins {
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.8.10'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
    id 'io.github.gradle-nexus.publish-plugin' version '1.2.0'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.10'
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding 'UTF-8'

group 'me.dkim19375'
version '1.4.0'

//noinspection GrUnresolvedAccess
compileKotlin.kotlinOptions {
    languageVersion = "1.5"
    jvmTarget = '1.8'
}

//noinspection GrUnresolvedAccess
compileTestKotlin.kotlinOptions {
    languageVersion = "1.5"
    jvmTarget = '1.8'
}

license {
    header = rootProject.file 'LICENSE'
    include '**/*.kt'
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://m2.dv8tion.net/releases' }
    maven { url = 'https://repo.triumphteam.dev/snapshots/' }
}

dependencies {
    compileOnly 'com.charleskorn.kaml:kaml:0.52.0'
    compileOnly 'org.jetbrains.kotlin:kotlin-reflect:1.8.10'
    compileOnly 'me.mattstudios:triumph-config:1.0.5-SNAPSHOT'

    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.10'
    //noinspection GradlePackageUpdate
    api('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4') {
        exclude group: 'org.jetbrains.kotlin'
    }
    //noinspection GradlePackageUpdate
    api('org.jetbrains.kotlinx:kotlinx-coroutines-reactor:1.6.4') {
        exclude group: 'org.jetbrains.kotlin'
        exclude module: 'kotlinx-coroutines-core'
    }

    // optional dependencies
    //noinspection GradlePackageUpdate
    compileOnly 'commons-io:commons-io:2.11.0'
    compileOnly 'com.google.code.gson:gson:2.10.1'
    compileOnly 'org.apache.commons:commons-text:1.10.0'
    compileOnly 'org.apache.commons:commons-lang3:3.12.0'

    // testing libs
    testImplementation 'commons-io:commons-io:2.11.0'
    testImplementation 'com.charleskorn.kaml:kaml:0.52.0'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-reflect:1.8.10'
    testImplementation 'me.mattstudios:triumph-config:1.0.5-SNAPSHOT'
    testImplementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'org.jetbrains.kotlin:kotlin-test:1.8.10'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar) {
    from dokkaJavadoc
    classifier 'javadoc'
}

publishing {
    final project = project
    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            groupId 'io.github.dkim19375'
            //noinspection GroovyAssignabilityCheck
            artifactId 'dkimcore'
            version project.version

            //noinspection GroovyAssignabilityCheck
            from components.kotlin
            //noinspection GroovyAssignabilityCheck
            artifact sourcesJar
            //noinspection GroovyAssignabilityCheck
            artifact javadocJar

            pom {
                //noinspection GroovyAssignabilityCheck
                name = 'DkimCore'
                //noinspection GroovyAssignabilityCheck
                description = 'A library for some utilities made in kotlin!'
                //noinspection GroovyAssignabilityCheck
                url = 'https://github.com/dkim19375/DkimCore'

                //noinspection GroovyAssignabilityCheck
                packaging = 'jar'

                licenses {
                    license {
                        //noinspection GroovyAssignabilityCheck
                        name = 'MIT License'
                        //noinspection GroovyAssignabilityCheck
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        //noinspection GroovyAssignabilityCheck
                        id = 'dkim19375'
                    }
                }

                scm {
                    //noinspection GroovyAssignabilityCheck
                    connection = 'scm:git:git://github.com/dkim19375/DkimCore.git'
                    //noinspection GroovyAssignabilityCheck
                    developerConnection = 'scm:git:ssh://github.com:dkim19375/DkimCore.git'
                    //noinspection GroovyAssignabilityCheck
                    url = 'https://github.com/dkim19375/DkimCore'
                }
            }
        }
    }
}

nexusPublishing {
    //noinspection GrUnresolvedAccess
    packageGroup.set 'io.github.dkim19375'
    repositories {
        //noinspection GrUnresolvedAccess
        sonatype {
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = project.findProperty("mavenUsername")
            password = project.findProperty("mavenPassword")
        }
    }
}

signing.sign publishing.publications.mavenJava

tasks.jar.dependsOn licenseFormat